apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def getProp(String propName, boolean quote) {
    def propsFile = rootProject.file('private/private.properties')
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        if (quote) {
            return '"' + props[propName] + '"'
        } else {
            return props[propName]
        }
    } else {
        return "";
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.github.geekarist.whereishome"
        minSdkVersion 18
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        buildConfigField('String', 'CITYMAPPER_API_KEY', getProp('citymapper.api.key', true))
        manifestPlaceholders = [
                PLACES_API_KEY    : getProp('places.api.key', false),
                DIRECTIONS_API_KEY: getProp('directions.api.key', false)
        ]
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.squareup.okhttp3:logging-interceptor:3.2.0'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.google.android.gms:play-services:8.4.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.squareup.retrofit2:retrofit:2.0.1'
    compile 'com.squareup.retrofit2:converter-gson:2.0.1'
    compile 'com.annimon:stream:1.0.9'

    androidTestCompile 'com.android.support:support-annotations:23.4.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
}
